/*================================================================================
a boucing ball on a stair
a single-step adjusts its position depending on the x-position of ball
method: linear-search
time-complexity: O(n)
================================================================================*/

INIT_X    <=>   (x = 0).
X_MOVE(v) <=> [](x' = v).
INIT_Y(h) <=>   (y = h  &  y' = 0).
FALL(g)   <=> [](y'' = -g).
WALL(w)   <=> [](x- = w  =>  x' = -x'-).

// a variable to store x-position of the single-step
INIT_CORNER      <=>   (cornerx = 0).
CORNER_MOVE      <=> [](cornerx' = 0).
DETECT_POS(left) <=> []( (left <= x- < left + 1)  =>  (cornerx = left) ).
STAIR_RANGE := { DETECT_POS(i) | i in {0..9} }.

BOUNCE_HOR <=> []((y- = 9 - cornerx) & (cornerx <= x- < cornerx + 1)  =>  (y' = -9/10 * y'-)).

INIT_X, INIT_Y(9 + 2), INIT_CORNER,
(CORNER_MOVE << STAIR_RANGE),
(FALL(9.8) << BOUNCE_HOR),
(X_MOVE(3) << (WALL(0), WALL(10))).

// #hylagi -p 3
