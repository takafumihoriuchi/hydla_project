// 2015/07/19
// Kenichi Betsuno
// #omit width, height, e, r, xmax, ymax

// width  : ステップの幅
// height : ステップの高さ
// e      : 反発係数
// r      : ボール半径
// xmax   : エスカレーターの右端座標

W := {w1..w5}. // ステップの角の x 座標
H := {h1..h5}. // ステップの角の y 座標
X := {x1..x3}. // ボールの x 座標
Y := {y1..y3}. // ボールの y 座標

CONST <=> [](width = 5 & height = 3 & e = 0.8 & r = 1 & xmax = (|H|-1)*width & ymax = (|W|-1)*height).

// 初期化
INIT_STEP(a,b,c) <=> a = b & [](a' = c).
INIT_X(a,b,c) <=> a = b & a' = c & [](a'' = 0).
INIT_Y(a,b,c) <=> a = b & a' = c & [](a'' = -10).

// ボールと階段の衝突
STEP_TOP(w,h,x,y) <=> [](w- <= x- <= w- + width & y- = h- => y' = -e*y'- + 2*h'-).
STEP_SIDE(w,h,x,y) <=> [](h- - height <= y- <= h- & x- = w- => x' = -e*x'- + 2*w'-).

LOOP(w,h) <=> [](w- >= xmax => w = -width & h = -height).
WALL(x,y) <=> [](x- = xmax | x- = 0 => x' = -x'- & y' = y'-).
DEATH(x,y) <=> [](y- <= -10 => x'=0 & y'=0).

CONST,
({INIT_X(X[i],xmax*i/(|X|+1),3*i/2) | i in {1..|X|}} << {STEP_SIDE(W[i],H[i],X[j],Y[j]) | i in {1..|W|}, j in {1..|X|}},
{INIT_Y(Y[i],ymax*i/(|Y|+1)+1,i/2) | i in {1..|Y|}} << {STEP_TOP(W[i],H[i],X[j],Y[j]) | i in {1..|W|}, j in {1..|X|}}) <<
({WALL(X[i],Y[i]) | i in {1..|X|}}, {DEATH(X[i],Y[i]) | i in {1..|X|}}),
({INIT_STEP(W[i],(i-2)*width,5/5) | i in {1..|W|}}, {INIT_STEP(H[i],(i-2)*height,3/5) | i in {1..|W|}}) <<
{LOOP(W[i],H[i]) | i in {1..|W|}}.
